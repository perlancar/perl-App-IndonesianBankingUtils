#!perl

# DATE
# DIST
# VERSION

use 5.010;
use strict;
use warnings;
use Log::ger;

use Log::ger::Level::FromEnv;
use Log::ger::Output ();

our %SPEC;

# we have to use this temporarily for now, this is not secure, but well ...
if (!defined($ENV{PERL_LWP_SSL_VERIFY_HOSTNAME})) {
    $ENV{PERL_LWP_SSL_VERIFY_HOSTNAME} = 0;
}

$SPEC{download_mandiri} = {
    v => 1.1,
    summary => 'Download Mandiri statements',
    args => {
        username  => {schema=>'str*', req=>1},
        password  => {schema=>'str*', req=>1},
        account   => {schema=>'str*'},
        data_dir  => {schema=>'str*'},
        log_dir   => {schema=>'str*'},
        save_dir  => {schema=>'str*'},
        mode      => {
            summary=>'Passed to Finance::Bank::ID::Mandiri constructor',
            schema=>'str*',
            default=>'',
        },
        days      => {schema=>'int*', default=>31},
    },
};

sub download_mandiri {
    require File::HomeDir;

    my %args = @_;

    my $data_dir = $args{data_dir} // File::HomeDir->my_home . "/mandiri";
    my $log_dir  = $args{log_dir}  // File::HomeDir->my_home . "/mandiri/logs";
    my $save_dir = $args{save_dir} // File::HomeDir->my_home . "/mandiri/logs/save";

    Log::ger::Output->set(
        'Composite',
        category_level => {Dumps => 'off'},
        outputs        => {
            Screen => {
                layout => [Pattern => {format=>'[%r] %m'}],
            },
            File => {
                conf => { path => "$log_dir/main.log" },
                layout => [Pattern => {format=>'[%d] [PID %P] %m'}],
            },
            DirWriteRotate => {
                conf  => { path => "$log_dir/dumps" },
                level => 'off',
                category_level => {Dumps => 'trace'},
            },
        },
    );

    require File::Path;
    File::Path::mkpath($data_dir) unless -d $data_dir;
    die "Can't create data_dir `$data_dir'" unless -d $data_dir;

    log_info("Start session");

    require Finance::Bank::ID::Mandiri;
    my $ibank = Finance::Bank::ID::Mandiri->new(
        username     => $args{username},
        password     => $args{password},
        logger       => Log::ger->get_logger,
        logger_dump  => Log::ger->get_logger(category => "Dumps"),
        verify_https => 1,
        save_dir     => $save_dir,
        mode         => $args{mode},
    );

    eval {
        my $bal = $ibank->check_balance;
        log_debug("Balance: $bal");

        my $stmt = $ibank->get_statement(
            account    => $args{account},
            days       => $args{days},
            parse_opts => {return_datetime_obj=>0},
        );
        my $filename = sprintf("%s/mandiri.statement.%s.%s.to.%s.yaml",
                               $data_dir,
                               $stmt->{account},
                               $stmt->{start_date},
                               $stmt->{end_date});

        # insert balance to statement (since mandiri statement doesn't contain
        # any balance information)
        $stmt->{_balance} = $bal;

        $filename =~ s/[: ]//g; # : is for windows
        log_info("Writing statements to YAML file `$filename' ...");
        require File::Slurper;
        require YAML::Syck;
        File::Slurper::write_text($filename, YAML::Syck::Dump($stmt));
    };

    if ($@) {
        log_error("die: $@");
    }

    # no matter what, try to logout so we're not locked out for 10 minutes,
    # annoying
    eval { $ibank->logout };

    log_info("End session");
    [200];
}

require Perinci::CmdLine::Lite;
Perinci::CmdLine::Lite->new(
    url => '/main/download_mandiri',
    extra_urls_for_version => ['/Finance/Bank/ID/Mandiri/'],
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

First, put your username and password in C<~/download-mandiri.conf>, e.g.:

 username = ABCDEF0123
 password = 123456

Then:

 % download-mandiri

Get your statements in data dir (defaults to C<~/mandiri/>). See logs in your
log dir (defaults to C<~/mandiri/logs>).


=head1 DESCRIPTION

This is a command-line script which you can run from cron or whatever to
conveniently download Bank Mandiri statements. By default, it downloads 1
month's worth of statements to C<~/mandiri/>. To change this, use C<--days> and
C<--data_dir>.


=head1 SEE ALSO

L<Finance::Bank::ID::Mandiri>

=cut
